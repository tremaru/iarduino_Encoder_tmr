// ВЫВОДИМ НАПРАВЛЕНИЕ ПОВОРОТА С 7 ЭНКОДЕРОВ:    //
// Поворот по     чс: Выводим '<'.                //
// Поворот против чс: Выводим '>'.                //  Если перепутать выводы A и B, то выводимые символы поменяются местами.
                                                  //
#include <iarduino_Encoder_tmr.h>                 //  Подключаем библиотеку iarduino_Encoder_tmr для работы с энкодерами через аппаратный таймер
iarduino_Encoder_tmr enc[7]={{3,4},{5,6},{7,8},{9,10},{11,12},{A0,A1},{A2,A3}}; //  Объявляем массив объектов enc для семи энкодеров указывая пары выводов каждого энкодера {№ вывода A, № вывода B}.
                                                  //  Библиотека iarduino_Encoder_tmr позволяет подключить до 8 энкодеров.
void setup(){                                     //
     Serial.begin(9600);                          //  Инициируем передачу данных в монитор последовательного порта на скорости 9600 бит/сек.
     for(uint8_t i=0; i<7; i++){enc[i].begin();}  //  Инициируем работу всех энкодеров.
}                                                 //
                                                  //
void loop(){                                      //
     String txt=" ";                              //  Определяем строку для вывода в монитор.
     for(uint8_t i=0; i<7; i++){                  //  Выполняем цикл для каждого энкодера.
         if( i ){ txt+=" | "; }                   //
         switch( enc[i].read() ){                 //  Читаем состояние очередного энкодера.
             case encLEFT : txt+='<'; break;      //  Если энкодер зафиксировал поворот влево,  добавляем символ '<'.
             case encRIGHT: txt+='>'; break;      //  Если энкодер зафиксировал поворот вправо, добавляем символ '>'.
             default      : txt+='-';             //  Если энкодер не зафиксировал поворот,     добавляем символ '-'.
         }                                        //
     }                                            //
     if(txt.indexOf('<')>=0||txt.indexOf('>')>=0){//  Если хотя бы один из энкодеров зафиксировал поворот (в строке есть символ '<' или '>'), то ...
         Serial.println(txt);                     //
     }                                            //
}                                                 //

// ПРИМЕЧАНИЕ:
// Если вы не любите работать с массивами,
// то можно создать отдельные объекты для каждого энкодера
// как это сделано в примере "Several_Encoders".